apply plugin: 'maven'

configurations {
  deployerJars
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: mavenUrl)

      pom {
        groupId = projectGroupId

        project {
          artifactId = libraryArtifactId
          name libraryName
          version = libraryVersion
          description = libraryDescription
          url = projectGitUrl

          licenses {
            license {
              name licenseName
              url licenseUrl
              distribution licenseDistribution
            }
          }
        }
      }
    }
  }
}

if (project.getPlugins().hasPlugin("com.android.library")) {
  task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.sourceFiles
  }

  task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
  }
} else if (project.getPlugins().hasPlugin("java-library")) {
  task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier.set('javadoc')
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}
